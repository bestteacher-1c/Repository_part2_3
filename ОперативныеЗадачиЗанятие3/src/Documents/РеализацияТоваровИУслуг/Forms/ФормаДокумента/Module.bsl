
////////////////////////////////////////////////////////////////////////////////
// РЕДАКТИРОВАНИЕ СТРОКИ ТЧ

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекДанные.Номенклатура) Тогда
		ТекДанные.Цена = ПолучитьЦенуТовара(ТекДанные.Номенклатура, Объект.Дата);
	Иначе
		ТекДанные.Цена = 0;
	КонецЕсли; 
	
	ВычислитьСуммуВТекущейСтрокеТЧ();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ВычислитьСуммуВТекущейСтрокеТЧ();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ВычислитьСуммуВТекущейСтрокеТЧ();
	
КонецПроцедуры


&НаКлиенте
Процедура ВычислитьСуммуВТекущейСтрокеТЧ()
	
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	ТекДанные.Сумма = ТекДанные.Цена * ТекДанные.Количество;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьЦенуТовара(Номенклатура, ДатаАктуальности)
	
	Возврат РегистрыСведений.НашиЦены.ПолучитьПоследнее(ДатаАктуальности, Новый Структура("Номенклатура", Номенклатура)).Цена;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// УСТАНОВКА ЦЕН ВО ВСЕХ СТРОКАХ ТЧ

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Товары) Тогда
		УстановитьСтандартныеЦеныВоВсехСтрокахТЧ();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаУстановитьЦены(Команда)
	
	Если ЗначениеЗаполнено(Объект.Товары) Тогда
		УстановитьСтандартныеЦеныВоВсехСтрокахТЧ();
	КонецЕсли; 
	
КонецПроцедуры


&НаСервере
Процедура УстановитьСтандартныеЦеныВоВсехСтрокахТЧ()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	НашиЦены.Номенклатура,
		|	НашиЦены.Цена
		|ИЗ
		|	РегистрСведений.НашиЦены.СрезПоследних(&ДатаАктуальности, Номенклатура В (&СписокТоваров)) КАК НашиЦены";
	
	Запрос.УстановитьПараметр("ДатаАктуальности", Объект.Дата);
	
	ВспомогательнаяТЗ = Объект.Товары.Выгрузить( , "Номенклатура");
	ВспомогательнаяТЗ.Свернуть("Номенклатура");
	Запрос.УстановитьПараметр("СписокТоваров", ВспомогательнаяТЗ.ВыгрузитьКолонку("Номенклатура"));
	
	ТаблицаЦен = Запрос.Выполнить().Выгрузить();
	ТаблицаЦен.Индексы.Добавить("Номенклатура"); // Для ускорения поиска
	
	Для каждого СтрокаТЧ Из Объект.Товары Цикл
		
		СтрокаТаблицыЦен = ТаблицаЦен.Найти(СтрокаТЧ.Номенклатура, "Номенклатура");
		АктуальнаяЦена   = ?(ЗначениеЗаполнено(СтрокаТаблицыЦен), СтрокаТаблицыЦен.Цена, 0);
		
		СтрокаТЧ.Цена  = АктуальнаяЦена;
		СтрокаТЧ.Сумма = СтрокаТЧ.Цена * СтрокаТЧ.Количество;
		
	КонецЦикла; 
	
КонецПроцедуры


//------Команда "Резерв"--------------------------------------------------

&НаКлиенте
Процедура Резерв(Команда)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) = Ложь Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("РезервЗавершение", ЭтотОбъект)
		, "Резервировать товары можно только после записи документа. Записать документ?"
		,РежимДиалогаВопрос.ДаНет
		,15
		,КодВозвратаДиалога.Да
		,"Документ не записан!"
		,КодВозвратаДиалога.Нет);
		
		
        Возврат;
		
	КонецЕсли;
	
	РезервНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РезервЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		Записать(Новый Структура("РежимЗаписиДокумента", РежимЗаписиДокумента.Запись));
		
		РезервНаСервере();
		
	КонецЕсли;

КонецПроцедуры


&НаСервере
Процедура РезервНаСервере()
	
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	
	ДокОбъект.ЗарезервироватьТовары();
	

КонецПроцедуры // Резерв_НаСервере()
